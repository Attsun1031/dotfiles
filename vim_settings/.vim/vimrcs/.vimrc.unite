" unite
" The prefix key.
nnoremap    [unite]   <Nop>
nmap    f [unite]

nnoremap [unite]u  :<C-u>Unite<Space>

" 全部乗せ
nnoremap <silent> [unite]a  :<C-u>UniteWithCurrentDir -buffer-name=files buffer file_mru bookmark file<CR>
" ファイル一覧
nnoremap <silent> [unite]f  :<C-u>Unite -buffer-name=files file<CR>
" バッファ一覧
nnoremap <silent> [unite]b  :<C-u>Unite buffer<CR>
" レジスタ一覧
nnoremap <silent> [unite]r  :<C-u>Unite register<CR>
" 常用セット
nnoremap <silent> [unite]u  :<C-u>Unite buffer file_mru<CR>
" 最近使用したファイル一覧
nnoremap <silent> [unite]m  :<C-u>Unite file_mru<CR>
" 現在のバッファのカレントディレクトリからファイル一覧
nnoremap <silent> [unite]d  :<C-u>UniteWithBufferDir file<CR>

" diff アクションの追加
let diff_action = {
      \   'description' : 'diff',
      \   'is_selectable' : 1,
      \ }

function! diff_action.func(candidates)
  if len(a:candidates) == 1
    " カレントバッファとdiffを取る
    execute 'vert diffsplit ' . a:candidates[0].action__path
  elseif len(a:candidates) == 2
    " 選択されたファイルとdiffを取る
    execute 'tabnew ' . a:candidates[0].action__path
    execute 'vert diffsplit ' . a:candidates[1].action__path
  else
    " 3-way以上は非対応
    echo 'too many candidates!'
  endif
endfunction

call unite#custom_action('file', 'diff', diff_action)
unlet diff_action

" unite ウィンドウでのアクション
autocmd FileType unite call s:unite_my_settings()
function! s:unite_my_settings()"{{{
    " ESCキーを2回押すと終了する
    nmap <buffer> <ESC>      <Plug>(unite_exit)
    nmap <buffer> <ESC><ESC> <Plug>(unite_exit)
    imap <buffer> jj      <Plug>(unite_insert_leave)
    nnoremap <silent><buffer> <C-k> :<C-u>call unite#mappings#do_action('preview')<CR>
    imap <buffer> <C-w>     <Plug>(unite_delete_backward_path)
    " Start insert.
    let g:unite_enable_start_insert = 1

    " ウィンドウを縦に分割して開く
    nnoremap <silent> <buffer> <expr> <C-v> unite#do_action('right')
    inoremap <silent> <buffer> <expr> <C-v> unite#do_action('right')
endfunction"}}}

